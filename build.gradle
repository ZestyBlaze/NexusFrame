plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

version = project.mod_version
group = project.maven_group

repositories {
    maven { url 'https://maven.nucleoid.xyz' } //Polymer
}

dependencies {
    // Base
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // Polymer Virtual entities
    modImplementation include("eu.pb4:polymer-virtual-entity:${project.polymer_version}")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name "sapphoCompany"
            url "https://maven.is-immensely.gay/releases"
            credentials(org.gradle.api.credentials.PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

publishMods {
    changelog = file("CHANGELOG.md").text.replace("@VERSION@", project.mod_version)
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("quilt")
    file = remapJar.archiveFile
    displayName = "NexusFrame ${project.mod_version}"

    modrinth("modrinth") {
        featured = true
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "Dk0J1riW"
        minecraftVersions.add("1.21")
        requires {
            slug = "fabric-api"
        }
        embeds {
            slug = "polymer"
        }
    }
}